;; Example options for nvim-thyme; some of the values are different from the default ones.

(let [std-config (vim.fn.stdpath :config)
      std-fnl-dir? (vim.uv.fs_stat (vim.fs.joinpath std-config "fnl"))
      use-lua-dir? (not std-fnl-dir?)]
  {:compiler-options {:correlate true
                      ;; Comment out below to enable `vim` APIs and any others in
                      ;; compile time.
                      ;; :compilerEnv _G
                      ;; Emphasize the error position with the pair of the strings.
                      :error-pinpoint ["|>>" "<<|"]}
   ;; The directory relative to `(stdpath :config)` to manage your Fennel modules
   ;; like you manage Lua modules under lua/. The value only affects non-macro
   ;; modules; for Fennel macros, arrange `macro-path` option instead.
   :fnl-dir (if use-lua-dir? "lua" "fnl")
   ;; The path patterns for `fennel.macro-path` to find Fennel macro module
   ;; path. Relative path markers (`.`) are internally replaced with the paths on
   ;; &runtimepath filtered by the directories preceded by `?`, for example, in
   ;; `./fnl/?.fnl`.
   :macro-path (-> ["./fnl/?.fnlm"
                    "./fnl/?/init.fnlm"
                    "./fnl/?.fnl"
                    "./fnl/?/init-macros.fnl"
                    "./fnl/?/init.fnl"
                    ;; NOTE: Only the last items can be `nil`s without errors.
                    (when use-lua-dir? (.. std-config "/lua/?.fnlm"))
                    (when use-lua-dir? (.. std-config "/lua/?/init.fnlm"))
                    (when use-lua-dir? (.. std-config "/lua/?.fnl"))
                    (when use-lua-dir? (.. std-config "/lua/?/init.fnl"))]
                   (table.concat ";"))
   :max-rollbacks 5
   :notifier (let [noisy-notifier (case (or (pcall require :fidget)
                                            (pcall require :noice)
                                            (pcall require :notify))
                                    (true mod) mod.notify
                                    _ vim.notify)]
               (fn [msg ...]
                 ;; Extract scope
                 (case (msg:match "^thyme%((.-)%): ")
                   "watch/recompiler"
                   (noisy-notifier msg ...)
                   ;; You can suppress messages of specific scopes.
                   ;; scope (if (vim.startswith scope "mounted-rollback") nil
                   ;;           (vim.notify msg ...))
                   _
                   (vim.notify msg ...))))
   :command {;; Set it to nil to inherit the values from the root option.
             :compiler-options {:correlate false :error-pinpoint ["|>>" "<<|"]}
             :cmd-history {;; With parinfer integration and the options below,
                           ;; nvim-thyme will modify your command history with
                           ;; parinfer-ed command results for fennel wrapper
                           ;; Ex commands.
                           ;; "overwrite"|"append"|"ignore"
                           :method "overwrite"
                           ;; trailing-parens only affects command history when
                           ;; the method option is set to either "overwrite" or
                           ;; "append".
                           ;; "omit"|"keep"
                           :trailing-parens "omit"}}
   :keymap {:compiler-options {:correlate false :error-pinpoint ["|>>" "<<|"]}}
   :watch {:event [:BufWritePost :FileChangedShellPost]
           :pattern "*.{fnl,fnlm}"}})
